ltn12 = require "ltn12"
json or= require "cjson.safe"

STREAMED = "Streamed successfully"

class Plain
  new: (@api_key, apibase, @headers) =>
    if apibase
      @api_base = apibase

  create_stream_sink: (delta_callback, ctx) =>
    streamed = false
    (chunk, err) ->
      if chunk==nil
        if err
          delta_callback nil, err
        else
          ctx.response = streamed and STREAMED or ""
      else
        delta_callback chunk
        streamed = true

      chunk, err

  authorization: { "authorization", "Bearer %s" }

  _request: (method, path, payload, delta_callback, extra_headers) =>
    headers = "accept": delta_callback and "text/event-stream" or "application/json"
    local source
    if payload
      body = payload
      if type(payload)=="table"
        body = json.encode payload
        headers["content-type"] = "application/json"
      headers["content-length"] = #body
      source = ltn12.source.string body

    if @api_key
      {name, template} = @authorization
      headers[name or "authorization"] = template and template\format(@api_key) or @api_key

    for _headers in *{ @headers, extra_headers }
      if _headers then for k,v in pairs _headers
        headers[k\lower()] = v or nil -- false becomes nil

    out = {}
    sink = if delta_callback
      @create_stream_sink delta_callback, out
    else
      ltn12.sink.table out

    success, status_code, response_headers, status_line = @get_provider!.request {
      url: @api_base .. path
      :sink
      :source
      :method
      :headers
    }
    if success
      response = if delta_callback then out.response else table.concat out
      pcall -> response = assert json.decode response
      response, status_code, response_headers, status_line
    else
      nil, status_code

  formatErr: (meta) -> meta.statusline or meta.status_code

  request: (method, endpoint, payload, delta_callback, headers) =>
    if method\match"^[/?]"
      endpoint, payload, delta_callback, headers = method, endpoint, payload, delta_callback
      method = payload and "POST" or "GET"

    response, status_code, headers, statusline = @_request method, endpoint, payload, delta_callback, headers
    meta = setmetatable { :status_code, :headers, :statusline }, __tostring: @formatErr
    if response
      if status_code<200 or status_code>299
        meta.data = response if response~=STREAMED
        response = nil
    response, meta

  generate: (endpoint, payload, delta_callback, headers) =>
    @request "POST", endpoint, payload, delta_callback, headers

  providers: {
    http: "socket.http"
    https: "ssl.https"
  }

  get_provider: =>
    unless @provider
      scheme = @api_base\match"^(.-)://"
      provider = scheme and @providers[scheme]
      if provider
        @provider = require provider
      else
        error "unknown scheme: "..@api_base

    @provider


class LinesStream extends Plain
  separatorPattern: "^(.-)\r?\n(.-)$"

  consumeBufferRest: false

  create_stream_sink: (delta_callback, ctx) =>
    acc_buffer, streamed, accumulate_only = "", false, false

    (chunk, err) ->
      if chunk==nil
        if streamed
          if not err and @consumeBufferRest and acc_buffer~=""
            @process_line acc_buffer, delta_callback, ctx
          elseif err = err or acc_buffer~="" and not acc_buffer\match"data: ?%[DONE%]" and "buffer not empty"
            delta_callback nil, err, acc_buffer

        unless err
          ctx.response = streamed and STREAMED or acc_buffer

      else
        acc_buffer ..= chunk
        unless accumulate_only
          line = true
          while line
            line, rest = string.match acc_buffer, @separatorPattern
            if line
              if @process_line line, delta_callback, ctx
                acc_buffer = rest
                streamed = true
              --elseif streamed
              --  delta_callback nil, "Unable to parse line", line
              --  return nil, "Unable to parse line"
              else
                accumulate_only = true
                break

      chunk, err

  process_line: (line, delta_callback, ctx) =>
    if line=="" then return true
    @process delta_callback, line

  process: (delta_callback, data, extra) =>
    if data=="[DONE]" or data\match"^ -$" then return true
    success, parsed = pcall -> assert json.decode data
    if success
      if @is_valid parsed
        delta_callback parsed, nil, extra
      else
        delta_callback nil, "Unexpected json chunk", parsed

      true

  is_valid: (parsed) =>
    true -- stub

  defaultEndpoint: "/chat"

  generate: (endpoint, payload, delta_callback, headers) =>
    if type(endpoint)~="string"
      endpoint, payload, delta_callback, headers = @defaultEndpoint, endpoint, payload, delta_callback
    response, meta = @request "POST", endpoint, payload, delta_callback, headers
    if response and response~=STREAMED
      if type(response)~="table" or not (@is_valid_final or @is_valid) @,response
        meta.data, response = response, nil
    response, meta

  modelsData: {
    "data"   -- openai
    "models" -- cohere
    "result" -- cloudflare
    -- otherwise assume response root -- together, github
  }

  models: (endpoint) =>
    response, meta = @request "GET", endpoint or "/models"
    if response
      if type(response)=="table"
        return (-> for k in *@modelsData do if v = response[k] return v)! or response
      else
        meta.data = response
    nil, meta


class BasicSSE extends LinesStream
  process_line: (line, delta_callback) =>
    if line=="" or line\sub(1,1)==":" or line\match"^event:"
      true
    elseif data = line\match"^data: ?(.+)$"
      @process delta_callback, data


class SSE extends LinesStream
  process_line: (line, delta_callback, ctx) =>
    ctx.event = ctx.event or {}
    e = ctx.event

    -- https://html.spec.whatwg.org/multipage/server-sent-events.html#event-stream-interpretation
    if line==""
      if next e
        ctx.event = {} -- reset
        if e.data
          return @process delta_callback, e.data, e
        else
          unless e[""] -- comment
            delta_callback nil, "data not found", e
    else
      field, value = line\match"^(.-):%s?(.+)$"
      switch field
        when "data"
          e.data = e.data and e.data.."\n"..value or value
        when "event","id","retry","" --comment
          e[field] = value
        when nil
          unless ({event:1,id:1,retry:1,data:1})[line]
            return false -- do not try to parse SSE anymore
        else
          if field\match"^%w+$"
            delta_callback nil, 'Unknown field "%s" with value "%s"'\format(field, value), e
          return nil

    true


class OpenAI extends BasicSSE
  api_base: "https://api.openai.com/v1"

  defaultEndpoint: "/chat/completions"

  generate: (payload, delta_callback=nil) =>
    super payload, if payload.stream then delta_callback

  is_valid: (parsed) =>
    parsed.choices


class GoogleGemini extends BasicSSE
  api_base: "https://generativelanguage.googleapis.com/v1beta"

  authorization: { "x-goog-api-key" }

  generate: (payload, model, stream, delta_callback=nil) =>
    assert model, "Model not specified"
    task = stream and "streamGenerateContent" or "generateContent"
    endpoint = "/models/%s:%s%s"\format model, task, stream and "?alt=sse" or ""
    super endpoint, payload, if stream then delta_callback

  is_valid: (parsed) =>
    parsed.candidates

  modelsData: { "models" }


class GoogleInternal extends BasicSSE -- abstract class
  @@._cache = {}
  class GoogleAuth extends Plain
    headers:
      "Content-Type": "application/x-www-form-urlencoded"
      "User-Agent": "google-api-nodejs-client/9.15.1"
    api_base: "https://oauth2.googleapis.com"
    new: (refresh_token, client_id, client_secret) =>
      @payload = table.concat {
        "grant_type=refresh_token", "refresh_token="..refresh_token, "client_id="..client_id, "client_secret="..client_secret
      }, "&"

    refresh: (expiry) =>
      curtime = os.time!
      unless expiry and expiry>curtime
        with a=assert @request "POST", "/token", @payload
          .expires_at = curtime + .expires_in
      

  client_id: nil
  client_secret: nil
  headers: nil
  api_base: "https://cloudcode-pa.googleapis.com/v1internal"

  new: (@refresh_token, @api_base) =>
    assert @refresh_token, "refresh_token required"
    @auth = GoogleAuth @refresh_token, @client_id, @client_secret
    if data = @@._cache[@refresh_token]
      { api_key:@api_key, exp:@exp, project:@project } = data
    else
      @project = (assert @request "POST", ":loadCodeAssist", cloudaicompanionProject:"default-project").cloudaicompanionProject
      @@._cache[@refresh_token] = api_key:@api_key, exp:@exp, project:@project

  request: (...) =>
    if data = @auth\refresh @exp
      { access_token:@api_key, expires_at:@exp } = data
    super ...

  generate: (request, model, stream, delta_callback=nil) =>
    super stream and ":streamGenerateContent?alt=sse" or ":generateContent",
      {:model, project:@project, :request}, if stream then delta_callback

  is_valid: (parsed) =>
    parsed.response.candidates

  models: =>
    response, meta = @request "POST", ":fetchAvailableModels", project:@project
    if response
      if type(response)=="table"
        addId = (obj,name) -> with obj
          .name = name
        return [ addId model,name for name,model in pairs response.models ]
      else
        meta.data = response
    nil, meta


GoogleInternal.class = (name, mixin) =>
  @[name] or= class extends @
    require"moon".mixin_table @.__base, mixin
  @[name]


{
  :STREAMED
  :Plain
  :LinesStream
  :BasicSSE
  :SSE
  :OpenAI
  :GoogleGemini
  :GoogleInternal
}
